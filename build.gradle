plugins {
  id "org.sonarqube" version "2.8"
}

buildScan {
  termsOfServiceUrl = 'https://gradle.com/terms-of-service'
  termsOfServiceAgree = 'yes'
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'checkstyle'
  apply plugin: 'jacoco'

  group 'de.intave.yoml'
  version '1.0.0-SNAPSHOT'

  repositories {
    google()
    mavenLocal()
    mavenCentral()
  }

  dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.2.4'
  }

  test {
    useJUnitPlatform()
  }

  sourceCompatibility = '8'

  java {
    withSourcesJar()
    withJavadocJar()
  }

  checkstyle {
    maxWarnings = 0
    ignoreFailures = false
    toolVersion '8.27'
    checkstyleTest.enabled = false
  }
}

task codeCoverageReport(type: JacocoReport) {
  executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

  subprojects.each {
    sourceSets it.sourceSets.main
  }

  reports {
    xml.enabled = true
    html.enabled = true
    xml.destination = new File("${buildDir}/reports/jacoco/report.xml")
    html.destination = new File("${buildDir}/reports/jacoco/report.html")
  }
}

sonarqube {
  properties {
    property "sonar.projectKey", "intave_yoml"
  }
}